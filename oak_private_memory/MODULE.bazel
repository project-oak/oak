###############################################################################
# Bazel now uses Bzlmod by default to manage external dependencies.
# Please consider migrating your external dependencies from WORKSPACE to MODULE.bazel.
#
# For more details, please check https://github.com/bazelbuild/bazel/issues/18958
###############################################################################
module(name = "oak_private_memory")

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

git_repository = use_repo_rule("@bazel_tools//tools/build_defs/repo:git.bzl", "git_repository")

bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "aspect_bazel_lib", version = "2.19.3")
bazel_dep(name = "bazel_skylib", version = "1.8.2")
bazel_dep(name = "bazel_features", version = "1.39.0")
bazel_dep(name = "googletest", version = "1.16.0", repo_name = "com_google_googletest")
bazel_dep(name = "grpc", version = "1.72.0", repo_name = "com_github_grpc_grpc")
bazel_dep(name = "grpc-java", version = "1.71.0", repo_name = "io_grpc_grpc_java")
bazel_dep(name = "rules_foreign_cc", version = "0.15.0")
bazel_dep(name = "rules_java", version = "8.14.0")
bazel_dep(name = "rules_proto", version = "7.1.0")
bazel_dep(name = "rules_oci", version = "2.2.6")
bazel_dep(name = "rules_shell", version = "0.6.1")
bazel_dep(name = "rules_rust", version = "0.68.1")
bazel_dep(name = "rules_rust_wasm_bindgen", version = "0.68.1")
bazel_dep(name = "googleapis", version = "0.0.0-20251211-05f65958")
bazel_dep(name = "toolchains_llvm", version = "1.6.0")

# We can't depend on Oak fully as a bzlmod yet, but we can start to reference
# some things from it. But we need to provide a different repo_name for now so we don't clash with the one in WORKSPACE.
bazel_dep(name = "oak", version = "0.0.0", repo_name = "oak_bzlmod")
local_path_override(
    module_name = "oak",
    path = "..",
)

llvm = use_extension("@toolchains_llvm//toolchain/extensions:llvm.bzl", "llvm")
llvm.toolchain(
    name = "llvm_toolchain",
    llvm_version = "20.1.0",
)
llvm.sysroot(
    name = "llvm_toolchain",
    label = "@oak_cc_toolchain_sysroot//:sysroot",
    targets = ["linux-x86_64"],
)
use_repo(llvm, "llvm_toolchain")

register_toolchains("@llvm_toolchain//:all")

oak_toolchains = use_extension("@oak_bzlmod//bazel:extensions.bzl", "oak_toolchains")
use_repo(oak_toolchains, "umoci")

register_toolchains("@umoci//:umoci_toolchain")

oci = use_extension("@rules_oci//oci:extensions.bzl", "oci")
oci.pull(
    name = "distroless_cc_debian12",
    digest = "sha256:6714977f9f02632c31377650c15d89a7efaebf43bab0f37c712c30fc01edb973",
    image = "gcr.io/distroless/cc-debian12",
    platforms = ["linux/amd64"],
)
use_repo(oci, "distroless_cc_debian12", "distroless_cc_debian12_linux_amd64")

# Matches the version of protobuf used by Oak.
bazel_dep(name = "protobuf", version = "30.0", repo_name = "com_google_protobuf")

# https://github.com/bazel-contrib/rules_python
bazel_dep(name = "rules_python", version = "1.4.1", dev_dependency = True)

python = use_extension("@rules_python//python/extensions:python.bzl", "python", dev_dependency = True)

# Need ignore_root_user_error to run in docker container in CI
python.toolchain(
    ignore_root_user_error = True,
    is_default = True,
    python_version = "3.11",
)

# Oak Sysroot for toolchain.
# While we transition from WORKSPACE to bzlmod, it's not possible yet to depend
# on Oak as a module, and so not possible to get the sysroot yet at this point.
# So we just re-declare it here.
# Once we depend on Oak as a module, we can remove this and depend on it via the
# Oak module.

SYSROOT_SHA256 = "d6f608cf14b27bd4ae68f135b601b86bb9157a1a7a8fc08e43d7ff4ab7a18665"

http_archive(
    name = "oak_cc_toolchain_sysroot",
    build_file = "@oak_bzlmod//toolchain:sysroot.BUILD",
    sha256 = SYSROOT_SHA256,
    url = "https://storage.googleapis.com/oak-bins/sysroot/{}.tar.xz".format(SYSROOT_SHA256),
)

http_archive(
    name = "icu",
    build_file = "@//third_party/icu:icu.BUILD",
    # Remove the BUILD files because it will prevent configure_make to find
    # the files in directories containing the BUILD files.
    patch_cmds = ["cd source && find . -type f -name \"BUILD.bazel\" -delete"],
    strip_prefix = "icu",
    urls = ["https://github.com/unicode-org/icu/releases/download/release-73-1/icu4c-73_1-src.tgz"],
)

git_repository(
    name = "icing",
    build_file = "@//third_party/icing:BUILD.bazel",
    commit = "c8263cfd12eacefde39cf16b2326fc3ca3fe934f",  # 2025-11-17
    remote = "https://android.googlesource.com/platform/external/icing",
)

bazel_dep(name = "cxx.rs", version = "1.0.194")

crate = use_extension("@rules_rust//crate_universe:extensions.bzl", "crate")
crate.spec(
    package = "cxx",
    version = "1.0.194",
)
crate.from_specs(
    name = "pm_crates",
)
use_repo(crate, "pm_crates")
