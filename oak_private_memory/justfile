presubmit: verify-bazelisk style build-and-test

verify-bazelisk:
    rm -r ~/.cache/bazelisk
    bazel --version

build-and-test-and-copy: style build-and-test private-memory-server private-memory-enclave-app

build-and-test:
    bazel test  -c opt //...:all -- -//third_party/...

style:
    bazel build -c opt --keep_going --config=rustfmt --config=clippy "$@" //...:all -- -//third_party/...

format-rust:
    bazel run --config=rustfmt @rules_rust//:rustfmt


private-memory-server: (copy-artifact "private_memory_server" "private_memory_server")
private-memory-enclave-app: (copy-artifact "bundle.tar" "private_memory_enclave_app")
private-memory-enclave-bundle-tar: (copy-artifact "bundle.tar" "private_memory_enclave_bundle.tar")

# This matches the logic in the parent copy-artifact rule.
# See comment there for more details.
copy-artifact target dest:
    bazel build -c opt {{target}}
    mkdir --parents ../artifacts/binaries
    cp --force --preserve=timestamps --no-preserve=mode \
        `bazel cquery -c opt {{target}} --output files` \
        ../artifacts/binaries/{{dest}}
